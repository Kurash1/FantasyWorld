# Constants
new named_int POPULATION_PER_POP = 10_000
new named_float POPULATION_MONTHLY_GROWTH_RATE = 0.001

# Evaluated Values
new named_float POP_MULT = ( POPULATION_PER_POP / 1000 )

new script_variable devastation = {
	name = "Devastation"
}

new modifier_definition pop_growth_modifier = {
	name = "Pop Growth Modifier"
	is_percentage = yes
}
new script_variable var_pop_growth_modifier = {
	name = "Pop Growth Modifier"
}
new script_variable var_pop_growth_rate = {
	name = "Pop Growth Rate"
}

new script_variable local_pop_growth_modifier = {
	name = "Local Pop Growth Modifier"
}
new script_variable pop_growth_per_year = {
	name = "Pop Growth Per Year"
}
new script_variable pop_growth_per_decade = {
	name = "Pop Growth Per Decade"
}
new script_variable pop_growth_per_century = {
	name = "Pop Growth Per Century"
}
new script_variable pop_growth_to_end = {
	name = "Pop Growth To End"
}

new script_variable pops = {
	name = "Pops"
}
new script_variable population = {
	name = "Population"
}

new effect starting_population = {
	args = float #in thousands
	transpile = {
		new named_int $ = (args / POP_MULT)
		base_tax = 0
		base_production = $
		base_manpower = 0
		
		&pops := args
		&population := pops
		&population *= 1000
		delete = $
	}
}
new effect set_population = {
	args = float #in thousands
	transpile = {
		&pops := args
		&population := pops
		&population *= 1000
		
		adjust_development
	}
}

new struct census = {
	province prov
	float val
}
new effect define_census = {
	args = {
		population = list<census>
	}
	transpile = {
		foreach $1 in args:population {
			$1:prov:history +=! {
				starting_population = $1:val
			}
			census_setter +=! {
				$1:prov = {
					set_population = $1:val
				}
			}
		}
	}
}

new named_effect census_setter = {
	log = "Census Reset"
}
new effect generate_population = {
	args = {
		float desired # in thousands
		float weight # how uniform the development is, 0 is completely uniform
		random seed # for random population numbers
		list<province> provinces # ordered list from most populous to least
	}
	transpile = {
		new named_float $f_sum = 0
		new named_float $f_weight = 1
		
		foreach $prov in args:provinces {
			$f_sum += ( 1 / $f_weight )
			$f_weight += args:weight
		}
		
		$f_weight := 1
		
		foreach $prov in args:provinces {
			new named_float $population = ( ( 1 / $f_weight ) / $f_sum * args:desired + args:seed * 2 - 1 )
			$prov:history +=! {
				starting_population = $population
			}
			census_setter +=! {
				$prov = {
					set_population = $population
				}
			}
			
			$f_weight += args:weight
			delete = $population
		}
		
		delete = $f_sum
		delete = $f_weight
	}
}
new custom_transpile census_reset = {
	write_file "census_reset.txt" = {
		census_setter
	}
}